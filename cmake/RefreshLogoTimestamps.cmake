set(LOGO_FILES "")
foreach(var IN ITEMS LOGO_BINANCE LOGO_MEXC LOGO_UZX)
  if(DEFINED ${var} AND NOT "${${var}}" STREQUAL "")
    list(APPEND LOGO_FILES "${${var}}")
  endif()
endforeach()

if(NOT DEFINED STAMP_FILE OR "${STAMP_FILE}" STREQUAL "")
  message(FATAL_ERROR "STAMP_FILE is required")
endif()
string(REGEX REPLACE "^\"|\"$" "" STAMP_FILE "${STAMP_FILE}")

set(old "")
if(EXISTS "${STAMP_FILE}")
  file(READ "${STAMP_FILE}" old)
endif()

set(new "")
set(any_changed FALSE)

foreach(f IN LISTS LOGO_FILES)
  # MSBuild generators may pass quoted -D values; normalize.
  string(REGEX REPLACE "^\"|\"$" "" f "${f}")
  if(NOT EXISTS "${f}")
    message(WARNING "Logo file missing: ${f}")
    continue()
  endif()
  file(SHA256 "${f}" hash)
  string(APPEND new "${f}\t${hash}\n")
  if(old MATCHES "(^|[\r\n])${f}\t${hash}([\r\n]|$)")
    # unchanged
  else()
    set(any_changed TRUE)
  endif()
endforeach()

if(any_changed)
  foreach(f IN LISTS LOGO_FILES)
    if(EXISTS "${f}")
      file(TOUCH "${f}")
    endif()
  endforeach()
  get_filename_component(stamp_dir "${STAMP_FILE}" DIRECTORY)
  if(NOT "${stamp_dir}" STREQUAL "" AND NOT EXISTS "${stamp_dir}")
    file(MAKE_DIRECTORY "${stamp_dir}")
  endif()
  file(WRITE "${STAMP_FILE}" "${new}")
  message(STATUS "Logo assets changed; refreshed timestamps for RCC.")
endif()
